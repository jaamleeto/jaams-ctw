import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'java'
    id "de.undercouch.download" version "4.1.1"
}

group 'your.group'
version '1.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    
    maven { name = 'spigotmc-repo'; url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name = 'sonatype'; url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { name = 'jitpack'; url = 'https://jitpack.io' }
    maven { name = 'dmulloy2-repo'; url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = 'https://repo.extendedclip.com/releases/' }
    maven { name = 'nametagedit-repo'; url = 'https://ci.nametagedit.com/plugin/repository/everything/' }
    maven { name = 'enginehub-repo'; url = 'https://maven.enginehub.org/repo/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'  // Spigot API
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'     // ProtocolLib
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'             // Vault API
    compileOnly 'com.github.connorlinfoot:titleapi:1.8.1'      // TitleAPI
    compileOnly 'com.nametagedit:nametagedit:4.4.16'           // NameTagEdit
    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:6.1'     // WorldEdit
    compileOnly 'me.clip:placeholderapi:2.11.6'                // PlaceholderAPI
}

jar {
    archiveFileName = 'plugin.jar'
}

task buildLatestSpigot(type: Download) {
    description = "Descarga y construye la última versión de Spigot 1.8.8"
    
    src 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'
    dest 'build/btmp/BuildTools.jar'

    doLast {
        javaexec {
            main = "-jar"
            args = [ "BuildTools.jar", "--rev", '1.8.8' ]
            workingDir = "build/btmp"
        }

        copy {
            from file('build/btmp/spigot-1.8.8.jar')
            into file('run/')
        }

        delete files('build/btmp')
    }
}

task runSpigotServer(dependsOn: build) {
    description = "Ejecuta el servidor Spigot con el plugin"

    doLast {
        copy {
            from file('build/libs/plugin.jar')
            into file('run/plugins/')
        }

        javaexec {
            main = "-jar"
            args = [ "spigot-1.8.8.jar" ]
            workingDir = "run/"
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:-options"
}
